//===== rAthena Script =======================================
//= Jawaii (The Lovers' Paradise) Town
//===== By: ==================================================
//= jAthena (1.0)
//= DNett123 (1.1 - 1.5)
//= L0ne_w0lf
//===== Current Version: =====================================
//= 3.7
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Jawaii Town NPCs
//===== Additional Comments: =================================
//= 1.0 Done By jAthena
//= 1.1 Rough Translation [DNett123]
//= 1.2 Started Grammer Corrections [DNett123]
//= 1.3 Some Edits Thanks To Vidar & Fusion [DNett123]
//= 1.4 Edited Dancer, Thanks ceskil [DNett123]
//= 1.5 Fixed Script, and spelling errors, some thanks to Sparkles [DNett123]
//= 1.6 Final corrections (not complete), thanks to the public, and DNett123!
//= 1.6c added missing monsters. Still 1 type is missing [Lupus]
//= 1.7 Fixed a lot of typos [Nexon]
//= 1.8 Removed monster spawns, added aegis ep 8.5 spawns to npc/mobs/fields/jawaii.txt [MasterOfMuppets]
//= 2.0 Updated the npcs according to iRO [MasterOfMuppets]
//= 2.01 fixed missing @ at randomdrink, thanks to theultramage [Lupus]
//= 2.02 Fixed Classic Suite NPC warping directly above a warp [Evera]
//= 3.0 Rescripted to Aegis 10.3 standard. Contains all Jawaii-related NPCs. [L0ne_W0lf]
//= 3.1 Fixed bugs with getpartnerid() (it never returns 1), fixed some conditons,
//=     replaced getpartnerid() with ispartneron() at some tri-forks [Lupus]
//= 3.2 Reverted ispartneron() BACK to getpartnerid(). [L0ne_W0lf]
//=     None of the NPCs need the player's partner to be online.
//= 3.3 Just a small fix up on the Bartender. [L0ne_W0lf]
//= 3.3a Just a little typo error. [Samuray22]
//= 3.4 Added missing checkweights. [L0ne_W0lf]
//= 3.5 Replaced effect numerics with constants. [L0ne_W0lf]
//= 3.6 Fixed bartender so he no longer hangs. [L0ne_W0lf]
//= 3.7 Added Izlude RE coordinates. [Euphy]
//============================================================

prt_in,173,13,4	script	Customer#SoloHan	86,{
	mes "[SoloHan]";
	if (!getpartnerid()) {
		mes "Oh man...";
		mes "I think I'm drunk~";
		mes "^666666*Hiccup...!*^000000";
		next;
		mes "[SoloHan]";
		mes "Hey, you...!";
		mes "You understand, don't you?!";
		mes "Aren't you upset looking at all these happily married couples?!";
		mes "Yeah~? Me too!";
		next;
		mes "[SoloHan]";
		mes "How dare they show off their happiness in front of people like us--!! ^666666*Sniff*^000000 Just because they found everlasting love, they think they're better than we are?!";
		next;
		mes "[SoloHan]";
		mes "I remember when couples were polite and were lovey dovey behind closed doors. Nowadays they hold hands, and even cuddle in public. I mean, come on! Get a room!";
		next;
		mes "[SoloHan]";
		mes "I mean, ^666666*Sniff*^000000, it's not like,";
		mes "I'm lonely or anything. I don't neeeeeeeed a woman~!";
		mes "Right, Bachewcca!";
		next;
		mes "[Bachewcca]";
		mes "...!";
		emotion ET_BEST, getnpcid(0, "Customer#Bachewcca");
		next;
		mes "[SoloHan]";
		mes "It's doesn't really matter whether you're married";
		mes "or not, right buddy? Right?!";
		mes "Come on! Drink with me!!";
		next;
		mes "^3355FFSuddenly he treated me as a close friend. It's a little embarrassing, but it's not a bad idea to accept";
		mes "a free drink...^000000";
		next;
		mes "^3355FF* Gulp Gulp Gulp *^000000";
		percentheal -10,0;
		next;
		mes "[SoloHan]";
		mes "So, what do you say?";
		mes "Let's go somewhere";
		mes "with some real liquor.";
		while(1) {
			next;
			if (select("...One more drink.:...What kind of place is it?") == 1) {
				mes "[SoloHan]";
				mes "Yeah~!";
				mes "That's the spirit!";
				mes "Hey, Bachewcca...";
				mes "Let's drink!";
				next;
				mes "[Bachewcca]";
				mes "^666666*Grunt!*^000000";
				specialeffect EF_TALK_SCREAM,AREA,"Customer#Bachewcca";
				next;
				mes "[SoloHan]";
				mes "To...";
				mes "To being single!";
				mes "F-Forever!!!";
				next;
				mes "^3355FF* Gulp Gulp Gulp *^000000";
				percentheal -10,0;
				next;
				set .@jaw_roof,.@jaw_roof+2;
				mes "[SoloHan]";
				if (.@jaw_roof > 8) {
					mes "Whoa...";
					mes "You alright?";
					mes "You seem kind of drunk...";
					next;
					mes "[SoloHan]";
					mes "I guess now's the perfect time to have you sign this! Don't worry, I'll send you straight to paradise!";
					next;
					if (select("Sign:Refuse to Sign") == 1) {
						mes "[SoloHan]";
						mes "Alright!";
						mes "Take care!";
						mes "Hahaha!";
						close2;
						percentheal 100,0;
						break;
					}
					mes "[SoloHan]";
					mes "Oh...";
					mes "Man.";
					mes "So, you spoil parties";
					mes "like this all the time, eh?";
					close;
				}
			}
			if (.@jaw_roof > 6) {
				mes "[SoloHan]";
				mes "^666666*Hiccup!*^000000";
				mes "So you feel like having some";
				mes "real fun? Okay, then just sign over here. Count on me, I'll send you to paradise.";
				next;
				if (select("Sign:Refuse to Sign") == 1) {
					mes "[SoloHan]";
					mes "Alright~!";
					mes "Take care and have fun!";
					mes "Harass some couples over there for me, will you?";
					close2;
					percentheal 100,0;
					break;
				}
				mes "[SoloHan]";
				mes "Oh...";
				mes "Man.";
				mes "No wonder you're single. You can't even recognize a good time when it's right in front of you.";
				close;
			}
			set .@jaw_roof,.@jaw_roof+3;
			mes "[SoloHan]";
			mes "Drink, drink!";
			mes "That's not enough!";
			mes "Drink more, buddy!";
			next;
			mes "[SoloHan]";
			mes "To...";
			mes "To being single!";
			mes "FOR EVER.";
			next;
			mes "^3355FF*Gulp Gulp Gulp*^000000";
			percentheal -10,0;
			next;
		}
		warp "jawaii_in",44,124;
		end;
	}
	else if (getpartnerid()) {
		mes "Oh man...";
		mes "I think I'm drunk~";
		mes "*Hiccup!*";
		next;
		mes "[SoloHan]";
		mes "...Wha!?";
		mes "Oh man!";
		mes "Get outta my face!";
		if (Sex == SEX_MALE) {
			mes "You smell like,";
			mes "whupped boyfriend";
			mes "or something!";
		}
		else {
			next;
			mes "[SoloHan]";
			mes "I think I know a naggy wife";
			mes "when I see one! Go boss your";
			mes "hubby around or something!";
		}
		next;
		mes "[SoloHan]";
		mes "Leave me alone!";
		mes "I don't want any of your marital bliss to rub off on me. Come on, Bachewcca! Tell 'em their kind ain't welcome here!";
		next;
		mes "[Bachewcca]";
		mes "^666666*Grrr...!*^000000";
		specialeffect EF_THROWITEM,AREA,"Customer#Bachewcca";
		close;
	}
	mes "Oh man...";
	mes "I think I'm drunk~";
	mes "^666666*Hiccup!*^000000";
	next;
	mes "[SoloHan]";
	mes "What is that...?";
	mes "Is this the third time this week I've gotten plastered? Bachewcca, help me count!";
	close;
}

prt_in,170,14,0	script	Customer#Bachewcca	89,{
	mes "[Bachewcca]";
	mes "..............";
	mes "^666666*Gulp....gulp...*^000000";
	mes "Grrrrr!! That hit the spot!";
	emotion ET_CRY;
	close;
}
